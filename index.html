<!DOCTYPE html>
<html>
  <head>
    <title>perldelta - What's Changed in Perl From v5.8 to v5.20</title>
    <meta http-equiv="Content-Type" content="text/html; charset=UTF-8"/>
    <script src="remark/remark.min.js" type="text/javascript">
        {
            "highlightStyle": "solarized_light",
            "highlightLanguage": "no-highlight"
        }
    </script>
    <style type="text/css" media="screen">
/*
SOLARIZED HEX 16/8 TERMCOL XTERM/HEX L*A*B RGB HSB
--------- ------- ---- ------- ----------- ---------- ----------- -----------
base03 #002b36 8/4 brblack 234 #1c1c1c 15 -12 -12 0 43 54 193 100 21
base02 #073642 0/4 black 235 #262626 20 -12 -12 7 54 66 192 90 26
base01 #586e75 10/7 brgreen 240 #585858 45 -07 -07 88 110 117 194 25 46
base00 #657b83 11/7 bryellow 241 #626262 50 -07 -07 101 123 131 195 23 51
base0 #839496 12/6 brblue 244 #808080 60 -06 -03 131 148 150 186 13 59
base1 #93a1a1 14/4 brcyan 245 #8a8a8a 65 -05 -02 147 161 161 180 9 63
base2 #eee8d5 7/7 white 254 #e4e4e4 92 -00 10 238 232 213 44 11 93
base3 #fdf6e3 15/7 brwhite 230 #ffffd7 97 00 10 253 246 227 44 10 99
yellow #b58900 3/3 yellow 136 #af8700 60 10 65 181 137 0 45 100 71
orange #cb4b16 9/3 brred 166 #d75f00 50 50 55 203 75 22 18 89 80
red #dc322f 1/1 red 160 #d70000 50 65 45 220 50 47 1 79 86
magenta #d33682 5/5 magenta 125 #af005f 50 65 -05 211 54 130 331 74 83
violet #6c71c4 13/5 brmagenta 61 #5f5faf 50 15 -45 108 113 196 237 45 77
blue #268bd2 4/4 blue 33 #0087ff 55 -10 -45 38 139 210 205 82 82
cyan #2aa198 6/6 cyan 37 #00afaf 60 -35 -05 42 161 152 175 74 63
green #859900 2/2 green 64 #5f8700 60 -20 65 133 153 0 68 100 60
*/

body {
    font-size: 1.3em;
}

.slide .inverse {
    color: #839496; /* base0 */
    background-color: #002b36; /* base03 */
}

.slide {
    color: #657b83; /* base00 */
    background-color: #fdf6e3; /* base3 */
}

.slide pre, .slide code {
    color: #586e75; /* base01 */
    background-color: #eee8d5; /* base2 */
}

.slide .inverse pre, .slide .inverse code {
    color: #93a1a1; /* base1 */
    background-color: #073642; /* base02 */
}

a:link, a:visited, a:hover, a:active {
    color: #268bd2; /* blue */
}

.footnote {
    position: absolute;
    bottom: 3em;
}
.footnote.right {
    right: 3em;
}
.code {
    font-family: Monaco, monospace;
}
.code-half {
    float: left; width: 48%; margin: 1%;
}

h1, h2, h3 { text-align: center; }
h5, h6 { margin: 0.5em; font-weight: normal }

img { width: 100% }
    </style>
  </head>
  <body>
    <textarea id="source">
name: title
class: center, middle, inverse

# perldelta 
## The Good Parts
### v5.8 - v5.20

.footnote.right[Chicago.PM]
---
class: center, middle
Perl 5.8 was released July 18, 2002

![Perl 5.8 was released July 18, 2002](images/perl-5.8-announcement.png)

Facebook was launched in 2004
---
class: center, middle
Perl 5.8.8 was released January 31, 2006

![Perl 5.8.8 was released January 31, 2006](images/perl-5.8.8-announcement.png)

Twitter was launched in July 2006
---
class: middle

# Perl Releases

* 5.10 - 2007-12-18
* 5.12 - 2010-04-12
* 5.14 - 2011-05-14
* 5.16 - 2012-05-20
* 5.18 - 2013-05-18
* 5.20 - 2014

---
class: middle

# OS Perl versions:

* Debian Squeeze (6.0 - 2011-02-06): 5.10.1
* Debian Wheezy (7.0 - 2013-05-04): 5.14.2
* Debian Jessie (8.0 - Developing): 5.18.2
* Ubuntu Lucid (10.04LTS - 2010-04): 5.10.1
* Ubuntu Precise (12.04LTS - 2012-04): 5.14.2
* Ubuntu Saucy (13.10 - 2013-10): 5.14.2
* RHEL 6 (2010-11-10): 5.10? 5.12?
* RHEL 7 (Fedora 19 - 2013-07-02): 5.16.3
---
class: center, middle, inverse

# Perl 5.10
## 2007-12-18
---
class: middle, center

# `use feature`
## Import new features

---
class: middle

# Import a single feature

```perl
use feature qw( say );

say "Hello, World!";
```
---
class: middle

# Import a bundle of features

```perl
use feature qw( :5.10 );

given ( $^V ) {
    when ( $_ lt v5.14 ) {
        say "Hello, history!";
    }
    when ( $_ gt v5.14 ) {
        say "Hello, future!";
    }
}
```
---
class: middle, center

# `say`
## Print with newline

`use feature qw( say )`

`use feature qw( :5.10 )`

---
class: middle

# Say

```perl
print "Hello, World!\n";
```

```perl
use feature qw( say );

say "Hello, World!";
```
---
class: middle, center

# `state` variables
## Keep a value between calls to a sub

`use feature qw( state )`

`use feature qw( :5.10 )`

---
class: middle
# State
.code-half[
.center[Before]
```perl
my $i = 0;
sub plus_i {
    print "$i\n";
    $i++;
}
plus_i() for 1..5;
```
]
.code-half[
.center[After]
```perl
use feature qw( state say );
sub plus_x {
    state $x = 0;
    say $x;
    $x++;
}
plus_x() for 1..5;
```
]
---
class: middle, center

# `//`
## Defined-or

No use required
---

# Defined-or
```perl
use feature qw( say );

sub false_default {
    say shift || "Default";
}

sub defined_default {
    say shift // "Default";
}

false_default( 1 );
false_default( 0 );
false_default( );

defined_default( 1 );
defined_default( 0 );
defined_default( );
```

.center[Fun fact: This syntax highlighter doesn't understand defined-or!]
---
class: middle, center

# `given/when`
## Switch

`use feature qw( switch )`

`use feature qw( :5.10 )`

---
class: middle
.code-half[
# If
```perl
sub condition {
    my ( $foo ) = @_;

    if ( $foo =~ /^FX/ ) {
        say "Foreign Exchange!";
    }
    elsif ( $foo =~ /^UST/ ) {
        say "US Treasuries";
    }
    else {
        say "Unknown!";
    }
}
```
]
.code-half[
# Given
```perl
use feature qw( switch );
sub given_when {
    my ( $foo ) = @_;
    given ( $foo ) {
        when ( /^FX/ ) {
            say "Foreign Exchange";
        }
        when ( /^UST/ ) {
            say "US Treasuries";
        }
        default {
            say "Unknown!";
        }
    }
}
```
]
---
class: middle

# For/When
```perl
use feature qw( switch );
sub for_when {
    for ( @_ ) {
        when ( /^FX/ ) {
            say "Foreign Exchange";
        }
        when ( /^UST/ ) {
            say "US Treasuries";
        }
        default {
            say "Unknown!";
        }
    }
}
```

---
class: middle, center

# Module::CoreList
## Perl core modules, features, and releases

`corelist`

---
class: middle

# corelist
* `corelist Module` - Which Perl first had Module?
* `corelist -f X` - Which feature bundle is X in?
* `corelist -r` - Perl releases
---
class: center, middle, inverse

# Perl 5.12
## 2010-04-12

---
class: center, middle

# `...`
## Yada-yada-yada

No `use feature` needed

---
class: middle

# Yada-yada-yada
```perl
use feature qw( say );

sub foo { die "Unimplemented" }

eval { foo(); };
say "foo(): $@";

sub bar { ... }

eval { bar(); };
say "bar(): $@";
```
---
class: center, middle

# `each @array; keys @array; values @array;`
No `use feature` needed
---
class: middle

# Arrays are Key/Value Pairs
```perl
use feature qw( say );

my @array = qw( foo bar baz );

say 'C-style for loop';
for ( my $i = 0; $i &lt; @array; $i++ ) {
    say "$i: $array[$i]";
}

say 'List of indicies';
for my $i ( 0..$#array ) {
    say "$i: $array[$i]";
}

say 'each @array';
while ( my ( $k, $v ) = each @array ) {
    say "$k: $v";
}

say 'keys @array';
say join ", ", keys @array;

say 'values @array';
say join ", ", values @array;
```
---
class: center, middle

# `autodie`
## Never forget to check errors again!

`use autodie`

---
class: middle

# autodie
```perl
sub old {
    open my $fh, '&lt;', 'DOESNOTEXIST'
        or die "Can't open 'DOESNOTEXIST' for reading: '$!'";
}

sub new {
    use autodie;
    open my $fh, '&lt;', 'DOESNOTEXIST';
}

use feature qw( say );

eval { old(); };
say "old(): $@";

eval { new(); };
say "new(): $@";
```
---

class: center, middle, inverse

# Perl 5.14
## 2011-05-14
---
class: center, middle
# `s///r`
## Non-destructive substitution

No `use feature` required
---
class: middle

# Non-destructive substitution
```perl
use feature qw( say );

sub old_change {
    my ( $to_change ) = @_;
    my $changed = $to_change;
    $changed =~ s/foo/bar/;
    say "$to_change -&gt; $changed";
}

sub new_change {
    my ( $to_change ) = @_;
    my $changed = $to_change =~ s/foo/bar/r;
    say "$to_change -&gt; $changed";
}

old_change( "foo" );
new_change( "foo" );
```
---
class: middle

# Best used in map()
```perl
use feature qw( say );

my @array = qw( fooo baro bazo );

sub old {
    say for map { my $x = $_; $x =~ s/o/0/; $x } @_;
}

sub new {
    say for map { s/o/0/r } @_;
}

say "old()";
old( @array );
say "new()";
new( @array );
```
---
class: center, middle

# More reliable exception throwing
## `DESTROY` can't change `$@`

---
class: center, middle

```
Ceci n'est pas un exemple
```
---
class: center, middle

# `HTTP::Tiny`
## Simple core HTTP client

`use HTTP::Tiny`
---
class: middle

# No more LWP::Simple

```perl
use feature qw( say );
use HTTP::Tiny;

my $resp = HTTP::Tiny-&gt;new-&gt;get( 'http://example.com' );
say $resp-&gt;{content};
```
---
class: center, middle

# `JSON::PP`
## Simple core JSON parser

`use JSON::PP`
---
class: middle
# JSON
```perl
use feature qw( say );
use JSON::PP;

my $json = '{ "foo": "bar", "baz": "fuzz" }';
my $hash = JSON::PP->new->decode( $json );

say "foo: " . $hash->{foo};
```
---
class: center, middle, inverse

# Perl 5.16
## 2012-05-26

---
class: center, middle

# `__SUB__`
## Reference to the current running sub

No `use feature` needed
---
class: middle
# Recurse Without Names
```perl
use feature qw( say );

my $old; $old = sub {
    my ( $i ) = @_;
    return $i if $i &lt;= 1;
    return $old-&gt;( $i - 1 ) + $old-&gt;( $i - 2 );
};
# Hello memory leak!

my $new = sub {
    my ( $i ) = @_;
    return $i if $i &lt;= 1;
    return __SUB__-&gt;( $i - 1 ) + __SUB__-&gt;( $i - 2 );
};

say $old-&gt;( 3 );
say $new-&gt;( 3 );
```
---
class: center, middle, inverse

# Perl 5.18
## 2013-05-18

---
class: center, middle

# `my sub`, `our sub`, `state sub`
## Lexical subroutines

`use feature qw( lexical_subs )`
---
class: middle
# My subs, Your subs
```perl
use feature qw( say lexical_subs );

sub old {
    say "old()";
    my $inner = sub {
        say "inner()";
    };
    $inner-&gt;();
}

sub new {
    say "new()";
    my sub inner {
        say "inner()";
    }
    inner();
}

old();
new();
```
---
class: middle
# Hide your subs
```perl
use feature qw( say lexical_subs );

sub new {
    say "new()";
    my sub inner {
        say "inner()";
    }
    inner();
}

sub hide {
    my sub new {
        say "hidden new()";
    }
    new();
}

new();
hide();
```
---

class: center, middle, inverse

# Perl 5.20
## Coming in 2014

---
class: center, middle

# `%hash{...}`, `%array[...]`
## Hash-based slices

No `use feature` needed
---
class: middle

# Hash/Array in, Hash out
```perl
use feature qw( say );

my %hash = ( foo =&gt; 0, bar =&gt; 1 );
my @array = qw( foo bar );

my %old_part_hash = map { $_ =&gt; $hash{$_} }
                    grep { $_ eq 'foo' }
                    keys %hash;

my %part_hash = %hash{"foo"};
say "part_hash{foo} -&gt; $part_hash{foo}";

my %part_array = %array[0];
say "part_array{0} -&gt; $part_array{0}";
```
---
class: center, middle

# `$hash_ref-&gt;%*`, `$ary_ref-&gt;@*`
## Postfix dereference operators

---
class: middle

# Read From Left to Right
```perl
my $href = {
    foo =&gt; "bar",
};
my $aref = [ "foo", "bar" ];

say %{ $href };
say $href-&gt;%*;

say @{ $aref };
say $aref-&gt;@*;
```
---
class: middle

# Deep Diving Dereference
```perl
say %{ $data-&gt;{results}[0]{symbols} };
```
```perl
say $data-&gt;{results}[0]{symbols}-&gt;%*
```
---
class: center, middle

# `sub foo( $bar, @baz );`
## Subroutine signatures

`use feature qw( signatures );`
---
class: middle
# Unpack @_ automatically
```perl
use feature qw( say signatures );

sub old {
    my ( $bar, @baz ) = @_;
    die "First argument missing" unless $bar;
    say "$bar -&gt; @baz";
}

sub new( $bar, @baz ) {
    say "$bar -&gt; @baz";
}

old( "foo" );
eval { old() };
say "old(): $@";

new( "foo" );
eval { new() };
say "new(): $@";
```
---
class: center, middle, inverse
# Questions!
    </textarea>
    <div id="slideshow"></div>
  </body>
</html>
